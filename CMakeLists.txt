cmake_minimum_required(VERSION 3.24)

project(Accounting VERSION 0.4.0 LANGUAGES CXX)

# This is needed to easily find the headers generated by MOC.
set(CMAKE_INCLUDE_CURRENT_DIR ON)

# Deal with all of the Qt specific stuff automatically.
set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

find_package(Qt6 REQUIRED COMPONENTS Widgets)
find_package(AWSSDK REQUIRED COMPONENTS s3)
find_package(fmt REQUIRED)

set(ACCOUNTING_SOURCES
    Widgets/BillListWidget.hpp
    Widgets/BillEditorDialog.hpp
    Widgets/TransactionEditorDialog.hpp
    Models/BillListModel.hpp
    Models/BillListModel.cpp
    Models/DatabaseModel.hpp
    Models/TransactionListModel.hpp
    Models/TransactionListModel.cpp
    Models/PocketMoneyProxyModel.hpp
    Models/PocketMoneyProxyModel.cpp
    Models/BillProxyModel.hpp
    Models/BillProxyModel.cpp
    Models/TransactionModel.hpp
    Models/TransactionModel.cpp
    Persistance/Database.hpp
    Persistance/S3.hpp
    MainWindow.hpp
    Util.hpp
    main.cpp

    Ui/TransactionEditorDialog.ui
    Ui/BillListWidget.ui
    Ui/MainWindow.ui
    Ui/BillEditorDialog.ui

    Notes/0001_form-layout-expands.md
    Notes/0002_understanding-model-view.md
    Notes/0003_generate-spreadsheet-odf.md
    Notes/0004_crash-binding-sigsegv.md
    Notes/0005_total-incorrect.md
    Notes/0006_crash-in-random-location.md

    Scripts/backup.sh

    LICENSE
    README.md
    TODO.md
)

qt_add_executable(Accounting
    MANUAL_FINALIZATION
    ${ACCOUNTING_SOURCES}
)

target_link_libraries(Accounting PRIVATE Qt6::Widgets ${AWSSDK_LINK_LIBRARIES} fmt::fmt)

# Automatically generate 'ui_${NAME}.h' for each 'Ui/${NAME}.ui' file.
set_target_properties(Accounting PROPERTIES
    AUTOUIC_SEARCH_PATHS "Ui"
)

# By default, all applications are console applications on windows.
set_target_properties(Accounting PROPERTIES
    WIN32_EXECUTABLE TRUE
)

qt_finalize_executable(Accounting)
